100 READING# = 0
200 WHILE READING# < 1000
300 OUT 64, 5 
400 READING# = READING# + 1
500 PRINT "-----------------------------"
600 PRINT "Reading: "; READING#
700 PORT = 71 : PDATA = 0 :   GOSUB 2000 : PRINT "Azure Sphere OS Version: "; RSTRING$
800 PORT = 70 : PDATA = 0 :   GOSUB 2000 : PRINT "Emulator version: "; RSTRING$
900 PORT = 63 : PDATA = 0 :   GOSUB 2000 : PRINT "Temperature: "; RSTRING$
1000 PORT = 63 : PDATA = 1 :   GOSUB 2000 : PRINT "Pressure: "; RSTRING$
1100 PORT = 63 : PDATA = 2 :   GOSUB 2000 : PRINT "Light: "; RSTRING$
1200 PORT = 64 : PDATA = 0 :   GOSUB 2000 : PRINT "X: "; RSTRING$
1300 PORT = 64 : PDATA = 1 :   GOSUB 2000 : PRINT "Y: "; RSTRING$
1400 PORT = 64 : PDATA = 2 :   GOSUB 2000 : PRINT "Z: "; RSTRING$
1500 PRINT "Sleep 1 seconds"
1600 OUT 30, 1 : WAIT 30, 1, 1
1700 WEND
1800 OUT 64, 4 : REM Stop the accelerometer
1900 END
2000 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
2100 OUT PORT, PDATA
2200 RSTRING$ = ""
2300 C=INP(200)
2400 WHILE C <> 0
2500 RSTRING$ = RSTRING$ + CHR$(C)
2600 C=INP(200)
2700 WEND
2800 RETURN
