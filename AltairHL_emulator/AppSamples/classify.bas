100 LEDSTATE = 0
200 READING# = 0
300 OUT 64, 3 : REM Start the accelerometer
400 WHILE READING# < 1E+06
500 READING# = READING# + 1
600 IF LEDSTATE = 0 THEN OUT 62,1 : LEDSTATE = 1 Else OUT 62,0 : LEDSTATE = 0
700 PRINT "-----------------------------"
800 PORT = 70 : PDATA = 0 :   GOSUB 1900 : PRINT "Emulator version: "; RSTRING$
900 PRINT "Reading: "; READING#
1000 PORT = 64 : PDATA = 0 :   GOSUB 1900 : PRINT "X: "; RSTRING$
1100 PORT = 64 : PDATA = 1 :   GOSUB 1900 : PRINT "Y: "; RSTRING$
1200 PORT = 64 : PDATA = 2 :   GOSUB 1900 : PRINT "Z: "; RSTRING$
1300 PORT = 64 : PDATA = 8 :  GOSUB 1900 : PRINT "Prediction: "; RSTRING$
1400 PRINT "Sleep 1 seconds"
1500 OUT 30, 1 : WAIT 30, 1, 1
1600 WEND
1700 OUT 64, 4 : REM Stop the accelerometer
1800 END
1900 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
2000 OUT PORT, PDATA
2100 RSTRING$ = ""
2200 C=INP(200)
2300 WHILE C <> 0
2400 RSTRING$ = RSTRING$ + CHR$(C)
2500 C=INP(200)
2600 WEND
2700 RETURN
