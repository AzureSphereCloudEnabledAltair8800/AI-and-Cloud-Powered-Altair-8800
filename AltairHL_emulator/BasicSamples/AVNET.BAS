100 READING# = 0
200 PRINT "The AVNET board must be level while the accelerometer is calibrated!"
300 OUT 30, 1 : WAIT 30, 1, 1 : REM Sleep so message is displayed
400 OUT 60, 1 : OUT 64, 3 : OUT 60, 0 : REM Red LED on, Start accelerometer inc calibrating, Red LED off
500 WHILE READING# < 1000
600 READING# = READING# + 1
700 PRINT "-----------------------------"
800 PORT = 70 : PDATA = 0 :  GOSUB 1900 : PRINT "Emulator version: "; RSTRING$
900 PRINT "Reading: "; READING#
1000 PORT = 63 : PDATA = 0 :  GOSUB 1900 : PRINT "Temperature: "; RSTRING$
1100 PORT = 63 : PDATA = 1 :  GOSUB 1900 : PRINT "Pressure: "; RSTRING$
1200 PORT = 63 : PDATA = 2 :  GOSUB 1900 : PRINT "Light: "; RSTRING$
1300 PORT = 64 : PDATA = 0 :  GOSUB 1900 : PRINT "X: "; RSTRING$
1400 PORT = 64 : PDATA = 1 :  GOSUB 1900 : PRINT "Y: "; RSTRING$
1500 PORT = 64 : PDATA = 2 :  GOSUB 1900 : PRINT "Z: "; RSTRING$
1600 PRINT "Sleep 1 seconds"
1700 OUT 30, 1 : WAIT 30, 1, 1
1800 WEND
1850 OUT 64, 4 : REM Stop the accelerometer
1860 END
1900 REM SUBROUTINE READS STRING DATA FROM PORT UNTIL NULL CHARACTER
2000 OUT PORT, PDATA
2100 RSTRING$ = ""
2200 C=INP(200)
2300 WHILE C <> 0
2400 RSTRING$ = RSTRING$ + CHR$(C)
2500 C=INP(200)
2600 WEND
2700 RETURN
